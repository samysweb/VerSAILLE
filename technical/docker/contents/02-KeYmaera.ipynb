{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Proving Safety of Control Envelopes in KeYmaera X\n",
    "As a first step, we want to prove the safety of an abstract model of Adaptive Cruise Control\n",
    "\n",
    "To this end, we prove the safety of a **control envelope** in KeYmaera X.\n",
    "Contrary to a concrete controller implementation, a control envelope allows for nondeterministic behavior and aims to show that *any* choice permissible according to the nondeterministic program guarantees safety of the system.\n",
    "\n",
    "Our system will have the following structure:\n",
    "- The system is described by two variables: Relative (!) Position $p_{\\text{rel}}$ and Velocity $v_{\\text{rel}}$\n",
    "- The Neural Network determines the acceleration of the car in the back via discrete control:\n",
    "  Accelerate ($a_{\\text{rel}}=-A$); No Acceleration ($a_{\\text{rel}}=0$); Brake ($a_{\\text{rel}}=B$)\n",
    "- The dynamics of the system are given as $p_{\\text{rel}}'=v_{\\text{rel}},v_{\\text{rel}}'=a_{\\text{rel}}$ and there is a control decision at least every $0.1$ seconds\n",
    "\n",
    "We can describe the dynamics of the system as the following hybrid program:\n",
    "```\n",
    "{prel'=vrel,vrel'=arel,t'=1&t<=T}\n",
    "```\n",
    "\n",
    "Our neural network will have three outputs (`x1`,`x2`,`x3`) and `arel` is then determined based of the maximum of these three cases.\n",
    "As a control envelope, this can be described as follows:\n",
    "```\n",
    "x1:=*;x2:=*;x3:=*;\n",
    "{\n",
    "    {?(x1>=x2&x1>=x3);arel:=-A;} ++\n",
    "    {?(x2>=x1&x2>=x3);arel:=0;} ++\n",
    "    {?(x3>=x2&x3>=x1);arel:=B;}\n",
    "}\n",
    "```\n",
    "Here `x1:=*` sets x1 to an arbitrary value.\n",
    "Subsequently we have a nondeterministic choice between 3 programs. Each program checks whether one of the three variables is maximal, e.g. `x1`, and then sets `arel` accordingly.\n",
    "Note, that this controller is **not** safe yet!\n",
    "Right now, it admits choosing any of the three accelerations in any of the states.\n",
    "To turn this into a control envelope with safety guarantees, we have to check whether the chosen acceleration is actually safe.\n",
    "We assume that braking is always safe (what else are we supposed to do in case of a crash?) so we will not add additional checks to the third option.\n",
    "However, for the first and second option, we must ensure that we can actually choose the given acceleration safely.\n",
    "For acceleration 0 this is easy: We basically just have to check whether we can let the system run for `T` more time while keeping the loop invariant intact.\n",
    "For acceleration `-A` we must take into account the loop invariant and ensure that it still holds *after* the current cycle.\n",
    "This results in the following conditions being added to our hybrid program:\n",
    "```\n",
    "x1:=*;x2:=*;x3:=*;\n",
    "{\n",
    "    {\n",
    "        ?(x1>=x2&x1>=x3);\n",
    "        ?(vrel - T*A >= 0 | \n",
    "                    prel + T * vrel + (-A) * T^2 / 2 >= (vrel + T * (-A))^2 / (2 * B) );\n",
    "        arel:=-A;\n",
    "    } ++\n",
    "    {\n",
    "        ?(x2>=x1&x2>=x3);\n",
    "        ?((vrel >= 0) | prel + T * vrel >= vrel^2 / (2 * B));\n",
    "        arel:=0;\n",
    "    } ++\n",
    "    {\n",
    "        ?(x3>=x2&x3>=x1);\n",
    "        arel:=B;\n",
    "    }\n",
    "};\n",
    "```\n",
    "\n",
    "With these preparations out of the way, we can now start formalizing our safety guarantee in KeYmaera X.\n",
    "To this end, we must create a new model of the following structure:\n",
    "```\n",
    "ArchiveEntry \"ACC-Simple\"\n",
    "ProgramVariables\n",
    "/* Here will be our program variable definitions */\n",
    "End.\n",
    "Definitions\n",
    "/* Here will be our auxilliary definitions as well as definitions of constants */\n",
    "End.\n",
    "Problem\n",
    "/* Here goes the guarantee we want to prove */\n",
    "End.\n",
    "End.\n",
    "```\n",
    "\n",
    "### Variables\n",
    "```\n",
    "ProgramVariables\n",
    "/* Network outputs */\n",
    "Real x1;\n",
    "Real x2;\n",
    "Real x3;\n",
    "/* Relative position of car */\n",
    "Real prel;\n",
    "/* Relative velocity of car */\n",
    "Real vrel;\n",
    "/* Relative acceleration of car */\n",
    "Real arel;\n",
    "/* Clock */\n",
    "Real t;\n",
    "End.\n",
    "```\n",
    "\n",
    "### Definitions\n",
    "```\n",
    "Definitions\n",
    "/* Time per loop iteration */\n",
    "Real T;\n",
    "/* Maximal acceleration */\n",
    "Real A;\n",
    "/* Braking acceleration */\n",
    "Real B;\n",
    "HP ctrl ::= { /* Here goes the control envelope we devised above. */ }\n",
    "HP plant ::= { /* Here goes the environment program we devised above */ }\n",
    "End.\n",
    "```\n",
    "\n",
    "### Problem\n",
    "To formalize the problem we also need to define the considered initial states as well as an invariant that encompasses the safe states of our system.\n",
    "To this end, consider that our system will always be safe so long as braking with full force avoids a collision. How can this be formaly expressed?\n",
    "```\n",
    "/* some preconditons */\n",
    "->\n",
    "[{\n",
    "    ctrl;\n",
    "    t:=0;\n",
    "    plant;\n",
    "}*@invariant(/* some invariant about the system */)] (/* the safety guarantee we want to provide */)\n",
    "```\n",
    "\n",
    "### Proof\n",
    "Once we have all these ingredients together, we can start proving the guarantee in KeYmaera X.\n",
    "To this end, we must prove that the formula above is a tautology.\n",
    "\n",
    "For convenience, the full Differential Dynamic Logic Model can be found [here](https://github.com/samysweb/VerSAILLE/tree/main/keymaerax/acc.kyx)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python_Versaille",
   "language": "python",
   "name": "python_versaille"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.20"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
